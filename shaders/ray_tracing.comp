#version 450 core

layout(local_size_x = 32, local_size_y = 32) in;

#define FLOAT_MAX 10000000.0
#define FLOAT_MIN -10000000.0
#define PI 3.1415926535897
#define INVPI 0.318309886183

struct Vertex {
	vec3 position;
	vec3 normal;
	vec3 tangent;
	vec3 bitangent;
	vec2 texcoord;
};

struct Material {
	vec3 emssive;
	vec3 baseColor;
	float subsurface, metallic, specular;
	float specularTint, roughness, anisotropic;
	float sheen, sheenTint, clearcoat;
	float clearcoatGloss, IOR, transmission;
};


struct Bound {
	vec3 pMin;
	vec3 pMax;
};

struct Triangle {
	ivec3 indices;
	int materialId;
	int textureId;
};

struct BVHNode {
	Bound bound;
	int axis;
	int rightChild;
	int startIndex;
	int endIndex;
};


struct Ray {
	vec3 origin;
	vec3 dir;
	float tMax;
};

struct Interaction {
	vec3 position;
	vec3 normal;
	vec2 texcoord;
	int materialId;
	int textureId;
	float time;
};

struct Camera{
	vec3 eye;
	vec3 lowerLeftCorner;
	vec3 horizontal;
	vec3 vertical;
};

#define VERTEX_VEC3_COUNT 5
#define MATERIAL_VEC3_COUNT 6
#define TRIANGLE_VEC3_COUNT 2
#define BVHNODE_VEC3_COUNT 4

layout(binding = 0, rgba32f) writeonly uniform image2D output_image;

layout(binding = 0) uniform samplerBuffer vertices;
layout(binding = 1) uniform samplerBuffer materials;
layout(binding = 2) uniform samplerBuffer triangles;
layout(binding = 3) uniform samplerBuffer bvh_nodes;

uniform sampler2D textures[20];

Vertex GetVertex(int i) {
	int offset = i * VERTEX_VEC3_COUNT;
	Vertex vertex;
	vertex.position  = texelFetch(vertices, offset + 0).rgb;
	vertex.normal    = texelFetch(vertices, offset + 1).rgb;
	vertex.tangent   = texelFetch(vertices, offset + 2).rgb;
	vertex.bitangent = texelFetch(vertices, offset + 3).rgb;
	vertex.texcoord  = texelFetch(vertices, offset + 4).rg;
	return vertex;
}

Material GetMaterial(int i) {
	int offset = i * MATERIAL_VEC3_COUNT;
	Material material;
	material.emssive   = texelFetch(materials, offset + 0).rgb;
	material.baseColor = texelFetch(materials, offset + 1).rgb;
	vec3 param1		   = texelFetch(materials, offset + 2).rgb;
	material.subsurface = param1[0];
	material.metallic = param1[1];
	material.specular = param1[2];
	vec3 param2        = texelFetch(materials, offset + 3).rgb;
	material.specularTint = param2[0];
	material.roughness = param2[1];
	material.anisotropic = param2[2];
	vec3 param3        = texelFetch(materials, offset + 4).rgb;
	material.sheen = param3[0];
	material.sheenTint = param3[1];
	material.clearcoat = param3[2];
	vec3 param4        = texelFetch(materials, offset + 5).rgb;
	material.clearcoatGloss = param3[0];
	material.IOR = param3[1];
	material.transmission = param3[2];
	return material;
}

Triangle GetTriangle(int i) {
	int offset = i * TRIANGLE_VEC3_COUNT;
	Triangle tri;
	tri.indices  = ivec3(texelFetch(triangles, offset + 0).rgb);
	ivec3 param  = ivec3(texelFetch(triangles, offset + 1).rgb);
	tri.materialId = param[0];
	tri.textureId  = param[1];
	return tri;
}

BVHNode GetBVHNode(int i) {
	int offset = i * BVHNODE_VEC3_COUNT;
	BVHNode node;
	node.bound.pMin = texelFetch(bvh_nodes, offset + 0).rgb;
	node.bound.pMax = texelFetch(bvh_nodes, offset + 1).rgb;
	vec3 param1     = texelFetch(bvh_nodes, offset + 2).rgb;
	node.axis = int(param1[0]);
	node.rightChild = int(param1[1]);
	node.startIndex = int(param1[2]);
	vec3 param2     = texelFetch(bvh_nodes, offset + 3).rgb;
	node.endIndex = int(param2[0]);
	return node;
}
uniform mat4 ScreenToWorld;
uniform vec3 CameraEye;
uniform int SCREEN_WIDTH;
uniform int SCREEN_HEIGHT;
uniform Camera camera;

layout(binding = 0) buffer debug_output{
	float debug_data[];
};

Ray CameraGetRay(float s, float t) {
	Ray ray;
	ray.origin = camera.eye;
	ray.dir = camera.lowerLeftCorner + s * camera.horizontal + t * camera.vertical - camera.eye;
	ray.tMax = FLOAT_MAX;
	return ray;
}

bool BoundIntersect(in Bound bound, in Ray ray) {
	vec3 invdir = 1.0 / ray.dir;

    vec3 f = (bound.pMax - ray.origin) * invdir;
    vec3 n = (bound.pMin - ray.origin) * invdir;

	/* 
		各个分量最早离开包围盒的时刻为光线离开包围盒的时刻
		各个分量最晚进入包围盒的时刻为光线进入包围盒的时刻
	*/
    vec3 tmax = max(f, n);
    vec3 tmin = min(f, n);
    float t1 = min(tmax.x, min(tmax.y, tmax.z));
    float t0 = max(tmin.x, max(tmin.y, tmin.z));

    return (t1 >= t0) ? true : false;
}

void swap(inout float f1, inout float f2) {
	float t = f1;
	f1 = f2;
	f2 = t;
}


// PBRT3中的三角形求交方法
bool TriangleIntersect(in Triangle tri, inout Ray ray, out Interaction isect) {
	const Vertex v0 = GetVertex(tri.indices[0]);
	const Vertex v1 = GetVertex(tri.indices[1]);
	const Vertex v2 = GetVertex(tri.indices[2]);

	vec3 p0 = v0.position;
	vec3 p1 = v1.position;
	vec3 p2 = v2.position;

	// 变换坐标系：光线原点在(0, 0, 0), 方向指向+z轴
	vec3 P0 = p0 - ray.origin;
	vec3 P1 = p1 - ray.origin;
	vec3 P2 = p2 - ray.origin;
	vec3 rd = ray.dir;
	// 光线z方向为0，选取另外两个最长的轴进行交换
	if (rd.z == 0) {
		if (abs(rd.x) > abs(rd.y)) {
			swap(P0.x, P0.z);
			swap(P1.x, P1.z);
			swap(P2.x, P2.z);
			swap(rd.x, rd.z);
		}
		else {
			swap(P0.y, P0.z);
			swap(P1.y, P1.z);
			swap(P2.y, P2.z);
			swap(rd.y, rd.z);
		}
	}
	// 错切变换，射线方向与+z轴对齐
	float invDz = 1.f / rd.z;
	P0.x = P0.x - P0.z * rd.x * invDz;
	P0.y = P0.y - P0.z * rd.y * invDz;
	P0.z *= invDz;
	P1.x = P1.x - P1.z * rd.x * invDz;
	P1.y = P1.y - P1.z * rd.y * invDz;
	P1.z *= invDz;
	P2.x = P2.x - P2.z * rd.x * invDz;
	P2.y = P2.y - P2.z * rd.y * invDz;
	P2.z *= invDz;

	// 边函数：由 p0 和 p1以及给定的第三点p指定的三角形面积的两倍
	float e0 = P1.x * P2.y - P1.y * P2.x;
	float e1 = P2.x * P0.y - P2.y * P0.x;
	float e2 = P0.x * P1.y - P0.y * P1.x;
	// 交点落在三角形外
	if ((e0 < 0 || e1 < 0 || e2 < 0) && (e0 > 0 || e1 > 0 || e2 > 0)) {
		return false;
	}

	// 交点落在三角形外
	if ((e0 < 0 || e1 < 0 || e2 < 0) && (e0 > 0 || e1 > 0 || e2 > 0)) return false;

	// 三角形三点共线
	float det = e0 + e1 + e2;
	if (det == 0) return false;

	/* 
		重心坐标：bi = ei / (e0 + e1 + e2) = bi / det
		插值后z = simga(bi * zi) = sigma(ei * zi) / det
		避免除法误差 判断z在光线范围内用sigma(ei * zi)与ray.tMax * det进行比较
	*/
	float tScaled = e0 * P0.z + e1 * P1.z + e2 * P2.z;
	if (det > 0 && (tScaled <= 0 || tScaled > ray.tMax * det)) return false;
	if (det < 0 && (tScaled >= 0 || tScaled < ray.tMax * det)) return false;

	// 重心坐标
	float invDet = 1.f / det;
	float t = tScaled * invDet;
	float b0 = e0 * invDet;
	float b1 = e1 * invDet;
	float b2 = e2 * invDet;
	
	vec2 uv0 = v0.texcoord;
	vec2 uv1 = v1.texcoord;
	vec2 uv2 = v2.texcoord;
	vec2 uvHit = uv0 * b0 + uv1 * b1 + uv2 * b2;

	vec3 normal0 = v0.normal;
	vec3 normal1 = v1.normal;
	vec3 normal2 = v2.normal;
	vec3 nHit;
	
	// 该三角形不存在法线
	if (normal0 == vec3(0) || normal1 == vec3(0) || normal2 == vec3(0)) {
		nHit = normalize(cross(p1 - p0, p2 - p0));
	} else {
		nHit = normal0 * b0 + normal1 * b1 + normal2 * b2;
	}

	// 光线击中三角形背面，需要反转法线
	if (dot(nHit, ray.dir) > 0) {
		nHit = -nHit;
	}

	isect.position = b0 * p0 + b1 * p1 + b2 * p2;
	isect.normal = nHit;
	isect.texcoord = uvHit;
	isect.textureId = tri.textureId;
	isect.materialId = tri.materialId;
	isect.time = t;
	ray.tMax = t; // 更新光线时间范围
	return true;
}

int debug_n;
bool BVHIntersect(inout Ray r, out Interaction isect) {
	// 把将要访问的节点压入栈中
	int nodeStack[128], top = 0;
	nodeStack[top++] = 0;
	// 判断击中标志
	bool hit = false;
	while (top > 0) {
		const int curId = nodeStack[--top];
		const BVHNode node = GetBVHNode(curId);
		// 未击中该点包围盒则跳过
		if (!BoundIntersect(node.bound, r)) continue;
		if (node.rightChild == -1) { // 叶子节点
			for (int i = node.startIndex; i < node.endIndex; ++i) { // 每个三角形求交
				if (TriangleIntersect(GetTriangle(i), r, isect)) {
					hit = true;
				}
			}
		} else { // 非叶子节点
			// 在划分轴上光线方向为负方向，则需要先访问右儿子
			if (r.dir[node.axis] < 0) {
				nodeStack[top++] = curId + 1;
				// 判断是否击中另一个儿子的包围盒
				BVHNode rc = GetBVHNode(node.rightChild);
				if (BoundIntersect(rc.bound, r)) nodeStack[top++] = node.rightChild;
			} else {
				nodeStack[top++] = node.rightChild;
				BVHNode lc = GetBVHNode(curId + 1);
				if (BoundIntersect(lc.bound, r)) nodeStack[top++] = curId + 1;
			}
		}
	}
	return hit;
}

void main() {
	ivec2 p = ivec2(gl_GlobalInvocationID.xy);
	Ray ray = CameraGetRay(float(p.x) / float(SCREEN_WIDTH), float(p.y) / float(SCREEN_HEIGHT));
	Interaction isect;
	if (BVHIntersect(ray, isect)) {
		vec3 color;
		if (isect.textureId != -1) {
			color = texture(textures[isect.textureId], vec2(isect.texcoord)).rgb;
		} else {
			color = GetMaterial(isect.materialId).baseColor;
		}
		imageStore(output_image, p, vec4(color, 1.0));
	} 
}